import com.android.build.gradle.BasePlugin as AndroidBasePlugin
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath libs.plugin.paparazzi
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

tasks.register('clean', Delete) {
  delete rootProject.buildDir
}

allprojects {
  plugins.withType(AndroidBasePlugin).configureEach {
    project.android {
      compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
      }
    }
  }
  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_11.toString()
    targetCompatibility = JavaVersion.VERSION_11.toString()
  }
  tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
      jvmTarget.set(JvmTarget.JVM_11)
      freeCompilerArgs.addAll([
        "-Xcontext-receivers",
        "-Xjvm-default=all", // TODO: IDE complains this isn't set. See transformableState.kt.
      ])
    }
  }
}
